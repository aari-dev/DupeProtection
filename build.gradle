plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'dev.aari.antidupe'
version = '1.0.0'
java.sourceCompatibility = JavaVersion.VERSION_21

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'
    implementation 'it.unimi.dsi:fastutil:8.5.12'
    implementation 'redis.clients:jedis:5.1.0'
}

shadowJar {
    minimize()
    relocate 'it.unimi.dsi.fastutil', 'dev.aari.antidupe.fastutil'
    relocate 'redis.clients.jedis', 'dev.aari.antidupe.jedis'
    relocate 'org.apache.commons.pool2', 'dev.aari.antidupe.pool2'
    archiveClassifier.set('')
    archiveFileName = "${project.name}-${project.version}.jar"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll(['-Xlint:deprecation', '-Xlint:unchecked'])
}

processResources {
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand(project.properties)
    }
}

build.dependsOn shadowJar